dnl ------------------------------------------------------------------------- 
dnl $Id: configure.in,v 1.177 2018/09/21 05:49:21 uehira Exp $
dnl -------------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR([src/win.c])
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE(WIN_pkg, 3.0.3)
AC_CONFIG_HEADERS([config.h])
AC_ARG_PROGRAM

dnl automake cygnus mode
dnl AM_MAINTAINER_MODE
dnl AC_EXEEXT
dnl AC_CYGWIN

dnl set default CFLAGS and FFLAGS
if test x"$CFLAGS" = x; then
	CFLAGS="-pipe -O2"
fi
if test x"$FFLAGS" = x; then
 	FFLAGS="-O2"
fi

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_F77
AC_PROG_AWK
AC_PROG_INSTALL
#AC_PROG_RANLIB
LT_INIT
#AC_PROG_LIBTOOL
#AC_CONFIG_MACRO_DIR([m4])
AM_PROG_CC_C_O
AC_LANG(C)

# FFLAGS : added "-fno-automatic -finit-local-zero" if they work.
AC_LANG_PUSH(Fortran 77)
my_save_fflags="$FFLAGS"
FFLAGS="-fno-automatic -finit-local-zero"
AC_MSG_CHECKING([whether F77 supports -fno-automatic -finit-local-zero])
# AC_COMPILE_IFELSE() did not work on SunOS 5.10 with Sun Fortran
AC_LINK_IFELSE([AC_LANG_PROGRAM([])],
	[AC_MSG_RESULT([yes])]
	[AM_ADD_FFLAGS="-fno-automatic -finit-local-zero"],
	[AC_MSG_RESULT([no])]
)
AC_LANG_POP(Fortran 77)
AC_SUBST([AM_ADD_FFLAGS])
FFLAGS="$my_save_fflags $AM_ADD_FFLAGS"

# FFLAGS : added "-static-libgcc" if they work.
AC_LANG_PUSH(Fortran 77)
my_save_fflags="$FFLAGS"
FFLAGS="-static-libgcc"
AC_MSG_CHECKING([whether F77 supports -static-libgcc])
AC_LINK_IFELSE([AC_LANG_PROGRAM([])],
	[AC_MSG_RESULT([yes])]
	[AM_ADD_FFLAGS1="-static-libgcc"],
	[AC_MSG_RESULT([no])]
)
AC_LANG_POP(Fortran 77)
AC_SUBST([AM_ADD_FFLAGS1])
FFLAGS="$my_save_fflags $AM_ADD_FFLAGS1"

dnl checks for libraries.
AC_CHECK_LIB(nsl, gethostbyname,
	[ac_cv_lib_nsl_gethostbyname="yes"], 
	[ac_cv_lib_nsl_gethostbyname="no"])
if test x"$ac_cv_lib_nsl_gethostbyname" = xyes; then
	LIBNSL="-lnsl"
else
	LIBNSL=""
fi
AC_SUBST(LIBNSL)

AC_CHECK_LIB(socket, connect,
	[ac_cv_lib_socket_connect="yes"],
	[ac_cv_lib_socket_connect="no"])
if test x"$ac_cv_lib_socket_connect" = xyes; then
	LIBSOCKET="-lsocket"
else
	LIBSOCKET=""
fi
AC_SUBST(LIBSOCKET)

dnl check libcygipc.a in case of Cygwin
LIBSHMGET=""
case $host_os in
cygwin*)
	AC_CHECK_LIB(cygipc, shmget,
		[ac_cv_lib_cygipc_shmget="yes"],
		[ac_cv_lib_cygipc_shmget="no"])
	if test x"$ac_cv_lib_cygipc_shmget" = xyes; then
		LIBSHMGET="-lcygipc"
	else
		AC_MSG_WARN(Test for libcygipc.a failed. Please install cygipc package and set LDFLAGS (if you needed).)
	fi
	;;
esac
AC_SUBST(LIBSHMGET)

# Checks for libraries.
AC_CHECK_LIB([m], [sqrt])

dnl Checks for header files.
AC_PATH_X
AC_PATH_XTRA
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_STAT

dnl by autoscan
AC_CHECK_HEADERS([inttypes.h limits.h memory.h stdlib.h string.h])

AC_CHECK_HEADERS([fcntl.h sys/file.h sys/ioctl.h sys/time.h syslog.h unistd.h stdarg.h varargs.h sys/param.h sys/vfs.h sys/statvfs.h stropts.h sys/ser_sync.h arpa/inet.h sys/mtio.h limits.h netdb.h netinet/in.h stddef.h sys/socket.h])
AC_CHECK_HEADERS(sys/mount.h,,,
[[#if HAVE_SYS_PARAM_H
# include <sys/param.h>
#endif
]])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SSIZE_T
AC_TYPE_UID_T
AC_TYPE_INT8_T
AC_TYPE_INT64_T
AC_TYPE_UINT8_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT64_T
AC_CHECK_TYPE(socklen_t,,
	[AC_DEFINE_UNQUOTED(socklen_t,int,
		[Define to 'int' if <sys/socket.h> does not define.])],
	[#include <sys/socket.h>])

AC_STRUCT_ST_BLOCKS
AC_STRUCT_TM
AC_CHECK_MEMBERS(struct tm.tm_gmtoff,,,[
#if TM_IN_SYS_TIME
#include <sys/time.h>
#else
#include <time.h>
#endif
])
AC_CHECK_MEMBER(struct statfs.f_bavail,,,[
#if HAVE_SYS_PARAM_H
#include <sys/param.h>
#endif
#ifdef HAVE_SYS_MOUNT_H
#include <sys/mount.h>
#endif
#ifdef HAVE_SYS_VFS_H
#include <sys/vfs.h>
#endif
])
dnl AC_STRUCT_TIMEZONE
AC_C_BIGENDIAN
AC_C_CHAR_UNSIGNED
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(char)
AC_CHECK_SIZEOF(float)
AC_CHECK_SIZEOF(double)
AC_CHECK_SIZEOF(int64_t)
AC_CHECK_SIZEOF(int *)

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_FUNC_FORK
AC_FUNC_MALLOC
#AC_FUNC_MKTIME
AC_FUNC_REALLOC
AC_TYPE_SIGNAL

dnl by autoscan
AC_CHECK_FUNCS([alarm bzero floor gethostbyname gettimeofday inet_ntoa memset mkdir pow rint rmdir select socket sqrt strchr strerror strrchr strtol tzset])

AC_CHECK_FUNCS([mktime timelocal select socket strerror strtol snprintf statfs64 statvfs mkdtemp])

dnl for !NetBSD
if test x"$ac_cv_member_struct_statfs_f_bavail" = xyes; then
   AC_CHECK_FUNCS([statfs])
fi

dnl check struct statfs.f_bavail size.
AC_MSG_CHECKING(size of struct statfs.f_bavail)
AC_RUN_IFELSE([AC_LANG_SOURCE([[
#include <sys/types.h>
#ifdef HAVE_SYS_PARAM_H
#include <sys/param.h>
#endif
#ifdef HAVE_SYS_MOUNT_H
#include <sys/mount.h>
#endif
#ifdef HAVE_SYS_VFS_H
#include <sys/vfs.h>
#endif
#include <stdlib.h>
#if (!defined(HAVE_STATFS) || defined(__SVR4)) && defined(HAVE_SYS_STATVFS_H)
#include <sys/statvfs.h> 
#define statfs statvfs
#define f_bsize f_frsize
#endif

int main()
{
  struct statfs  *sbuf;
  if (sizeof(sbuf->f_bavail) == sizeof(long))
     exit(0);
  exit(1);
}]])],
[
AC_MSG_RESULT(long) 
AC_DEFINE([STRUCT_STATFS_F_BAVAIL_LONG],1,[Define 'STRUCT_STATFS_F_BAVAIL_LONG'])
],
[
AC_RUN_IFELSE([AC_LANG_SOURCE([[
	#include <sys/types.h>
	#ifdef HAVE_SYS_PARAM_H
	#include <sys/param.h>
	#endif
	#ifdef HAVE_SYS_MOUNT_H
	#include <sys/mount.h>
	#endif
	#ifdef HAVE_SYS_VFS_H
	#include <sys/vfs.h>
	#endif
	#include <stdlib.h>
	#if (!defined(HAVE_STATFS) || defined(__SVR4)) && defined(HAVE_SYS_STATVFS_H)
	#include <sys/statvfs.h> 
	#define statfs statvfs
	#define f_bsize f_frsize
	#endif

	int main()
	{
	  struct statfs  *sbuf;
	    if (sizeof(sbuf->f_bavail) == sizeof(int64_t))
	         exit(0);
          exit(1);
	  }]])],
	  [
	  AC_MSG_RESULT(int64_t (long long))
	  AC_DEFINE([STRUCT_STATFS_F_BAVAIL_INT64],1,[Define 'STRUCT_STATFS_F_BAVAIL_INT64'])
	  ],
	  AC_MSG_ERROR(unkouwn type)
	  )
],
[
	case "$host" in
	      *-*-linux*)
	      AC_MSG_RESULT(long) 
	      AC_DEFINE([STRUCT_STATFS_F_BAVAIL_LONG],1,[Define 'STRUCT_STATFS_F_BAVAIL_LONG'])
	      ;;
	esac
])

dnl -------------------------------------------------------------------------
dnl check for ipv6 option (from http://www.itojun.org)
dnl -------------------------------------------------------------------------
AM_CONDITIONAL(IPV6, false)
AC_MSG_CHECKING(compile with IPv6 support specified)
AC_ARG_ENABLE([ipv6],
	[AS_HELP_STRING([--disable-ipv6],[Build WITHOUT ipv6 support.])],
	[], [enable_ipv6="yes"])
AC_MSG_RESULT($enable_ipv6)

dnl automated checks for ipv6 support.
if test x"$enable_ipv6" = xyes; then
	AC_MSG_CHECKING(for ipv6 support)
	AC_CACHE_VAL(ts_cv_ipv6, [dnl
	AC_TRY_COMPILE([#define INET6
#include <sys/types.h>
#include <netinet/in.h>],
		[int x = IPPROTO_IPV6; struct in6_addr a;],
		[ts_cv_ipv6="yes"], [ts_cv_ipv6="no"])])
	AC_MSG_RESULT($ts_cv_ipv6)
	if test x"$ts_cv_ipv6" = xyes; then
		AC_DEFINE([INET6],1,[Define 'INET6'])
		AM_CONDITIONAL(IPV6, true)
	fi
fi

dnl -------------------------------------------------------------------------
dnl debug option
dnl -------------------------------------------------------------------------
AC_MSG_CHECKING(if --enable-debug option specified)
AC_ARG_ENABLE([debug],
	[AS_HELP_STRING([--enable-debug],[Build a debugging version (default is no).])],
	[], [enable_debug="no"])
if test x"$enable_debug" = xyes; then
	AC_DEFINE([DEBUG],1,[Debugging mode])
	CFLAGS=" -Wall -pipe -g"
	FFLAGS=" -g $AM_ADD_FFLAGS $AM_ADD_FFLAGS1"
fi
AC_MSG_RESULT($enable_debug)

dnl -------------------------------------------------------------------------
dnl setting default sample size of 4 bytes
dnl -------------------------------------------------------------------------
AC_MSG_CHECKING(if default 4 byte sample size = 5)
AC_ARG_ENABLE([ssize5],
	[AS_HELP_STRING([--enable-ssize5],[Build default sample size = 5 version (New mode).])],
	[], [enable_ssize5="no"])
if test x"$enable_ssize5" = xyes; then
	AC_DEFINE([SSIZE5_MODE],1,[Default 4 byte sample size mode])
else
	AC_DEFINE([SSIZE5_MODE],0,[Default 4 byte sample size mode])
fi
AC_MSG_RESULT($enable_ssize5)

dnl -------------------------------------------------------------------------
dnl force use statfs64()
dnl -------------------------------------------------------------------------
AC_CHECK_FUNC([statfs64],
	[ac_statfs64="yes"],
	[ac_statfs64="no"])
#AC_MSG_RESULT($ts_cv_statfs64)
AC_MSG_CHECKING(if --enable-statfs64 option specified)
AC_ARG_ENABLE([statfs64],
	[AS_HELP_STRING([--enable-statfs64],[Use statfs64() instead of statfs() if it works.])],
	[], [enable_statfs64="no"])
AC_MSG_RESULT($enable_statfs64)
if test x"$enable_statfs64" = "xyes"; then
   if test x"$ac_statfs64" = "xyes"; then
      AC_DEFINE([USE_LARGE_FS],1,[Define 'USE_LARGE_FS'])
   else
      AC_MSG_WARN(--enable-statfs64 specified but not activated.)
   fi
fi

dnl -------------------------------------------------------------------------
dnl no compile fortran program option
dnl -------------------------------------------------------------------------
AM_CONDITIONAL(NO_FORTRAN, false)
AC_MSG_CHECKING(compile FORTRAN program (i.e. hypomh) specified)
AC_ARG_ENABLE([fortran],
	[AS_HELP_STRING([--disable-fortran],[Build WITHOUT fortran program.(i.e. hypomh)])],
	[], [enable_fortran="yes"])
if test x"$enable_fortran" = xno; then
	AM_CONDITIONAL(NO_FORTRAN, true)
fi
AC_MSG_RESULT($enable_fortran)

dnl -------------------------------------------------------------------------
dnl memory leak check option
dnl -------------------------------------------------------------------------
AM_CONDITIONAL(GC_MEMORY_LEAK_TEST, false)
AC_MSG_CHECKING(if --enable-memory-leak-check option specified)
AC_ARG_ENABLE([memory_leak_check],
	[AS_HELP_STRING([--enable-memory-leak-check],[Build a memory leak check version.])],
	[], [enable_memory_leak_check="no"])
AC_MSG_RESULT($enable_memory_leak_check)
if test x"$enable_memory_leak_check" = xyes; then
	AC_CHECK_HEADER(gc.h, [have_gc_header="yes"], [have_gc_header="no"])
	AC_CHECK_LIB(gc,GC_malloc,[have_gc_lib="yes"], [have_gc_lib="no"])

	if test x"$have_gc_header" = xyes && test x"$have_gc_lib" = xyes; then
		AC_DEFINE([GC_MEMORY_LEAK_TEST],1,[Define 'GC_MEMORY_LEAK_TEST'])
		CFLAGS="-DFIND_LEAK $CFLAGS"
		AM_CONDITIONAL(GC_MEMORY_LEAK_TEST, true)
	fi
fi


AM_CONDITIONAL(SUNOS4, false)
AM_CONDITIONAL(SOL2, false)
dnl AM_CONDITIONAL(X86, false)
AM_CONDITIONAL(FREEBSD, false)
case "$host" in
dnl ---------------------------------------
dnl in case of SunOS4 
dnl ---------------------------------------
  *-*-sunos4*)
   AC_DEFINE([SUNOS4],1,[Define 'SUNOS4'])
   AM_CONDITIONAL(SUNOS4, true)
dnl   X_CFLAGS="-I/usr/openwin/include"
dnl   X_LIBS="-L/usr/openwin/lib"
dnl   FLDFLAGS="-Bstatic"
dnl   if test -z "$GCC";then
dnl     LDFLAGS="-Bstatic $LDFLAGS"
dnl   else
dnl     LDFLAGS="-static $LDFLAGS"
dnl   fi
   ;;
dnl ---------------------------------------
dnl in case of Solaris2
dnl ---------------------------------------
  *-*-solaris2*)
   AM_CONDITIONAL(SOL2, true)
   ;;

dnl ---------------------------------------
dnl in case of X86
dnl ---------------------------------------
dnl  i386*-*-*)
dnl   AM_CONDITIONAL(X86, true)
dnl   ;;
dnl  i486*-*-*)
dnl   AM_CONDITIONAL(X86, true)
dnl   ;;
dnl  i586*-*-*)
dnl   AM_CONDITIONAL(X86, true)
dnl   ;;
dnl  i686*-*-*)
dnl   AM_CONDITIONAL(X86, true)
dnl   ;;
esac

dnl ---------------------------------------
dnl in case of FreeBSD
dnl ---------------------------------------
case $host_os in
freebsd*)
  AM_CONDITIONAL(FREEBSD, true)
  ;;
esac

dnl ---------------------------------------
dnl check bash
dnl ---------------------------------------
AC_PATH_PROGS(BASH, bash, :)
if test $BASH = ":"; then
	AC_MSG_WARN(Test for bash failed. Please install bash program.)
fi
       
dnl ---------------------------------------
dnl check perl
dnl ---------------------------------------
AC_PATH_PROGS(PERL, perl, :)
if test $PERL = ":"; then
	AC_MSG_WARN(Test for perl failed. Please install perl program.)
fi
       
dnl ---------------------------------------
dnl check wish
dnl ---------------------------------------
AC_PATH_PROGS(WISH, wish8.6 wish8.5 wish8.4 wish8.0jp wish4.2jp wish, :)
if test $WISH = ":"; then
	AC_MSG_WARN(Test for wish failed. Please install wish program.)
fi
       
dnl ---------------------------------------
dnl check tclsh
dnl ---------------------------------------
AC_PATH_PROGS(TCLSH, tclsh8.6 tclsh8.5 tclsh8.4 tclsh8.0jp tclsh7.6jp tclsh, :)
if test $TCLSH = ":"; then
	AC_MSG_WARN(Test for tclsh failed. Please install tclsh program.)
fi
       
dnl -------------------------------------------------------------------------
dnl X src compile or not
dnl -------------------------------------------------------------------------
AM_CONDITIONAL(NO_X_SRC, test x$no_x = xyes)

dnl -------------------------------------------------------------------------
dnl MT device check. check sys/mtio.h exists or not. (for fromtape, rtape, wtape)
dnl -------------------------------------------------------------------------
AM_CONDITIONAL(MT_HEADER, false)
AC_CHECK_HEADER(sys/mtio.h, [have_mt_header="yes"], [have_mt_header="no"])
if test x$have_mt_header = "xno"; then
	AC_MSG_WARN(sys/mtio.h does not exist.)
	AC_MSG_WARN(Fllowing programs are not compiled :fromtape rtape wtape)
else
	AM_CONDITIONAL(MT_HEADER, true)
fi

dnl **********************************
dnl output winsystem.h
dnl **********************************
AC_CONFIG_COMMANDS([src/winsystem.h],
[
	outfile=src/winsystem.h-tmp
	cat > $outfile << _EOF_
/* 
 *  winsystem.h
 *
 * -------------------------------------------------------------------------
 * DO NOT EDIT THIS FILE! 
 * This is a generated file.  Please modify 'configure.in'.
 * -------------------------------------------------------------------------
 */

#ifndef _WINSYSTEM_H_
#define _WINSYSTEM_H_

#include <sys/types.h>

#include <stdio.h>

_EOF_

	if test x$ts_cv_ipv6 = xyes; then
	   cat >> $outfile << _EOF_
/* Define 'INET6' */
#define INET6 1

_EOF_
	fi

	cat >> $outfile << _EOF_
/* WIN system headers */
#include <filter.h>
#include <pltxy.h>
#include <tcpu.h>
#include <udpu.h>
#include <ulaw.h>
#include <win_log.h>

_EOF_

	if test x$ac_cv_sizeof_char = x1; then
	   if test x$ac_cv_c_char_unsigned = xyes; then
	      cat >> $outfile << _EOF_
typedef signed char      int8_w;   /*   signed 1 byte integer */
_EOF_
	   else
	      cat >> $outfile << _EOF_
typedef char             int8_w;   /*   signed 1 byte integer */
_EOF_
	  fi
	  cat >> $outfile << _EOF_
typedef unsigned char   uint8_w;   /* unsigned 1 byte integer */
_EOF_
	fi

	if test x$ac_cv_sizeof_short = x2; then
	   cat >> $outfile << _EOF_
typedef short            int16_w;  /*   signed 2 byte integer */
typedef unsigned short  uint16_w;  /* unsigned 2 byte integer */
_EOF_
	fi

	if test x$ac_cv_sizeof_int = x4; then
	   cat >> $outfile << _EOF_
typedef int              int32_w;  /*   signed 4 byte integer */
typedef unsigned int    uint32_w;  /* unsigned 4 byte integer */
_EOF_
	fi

	cat >> $outfile << _EOF_

/* typedef uint32_w  WIN_blocksize; */
typedef uint32_w  WIN_bs;   /* win blocksize */
typedef uint16_w  WIN_ch;
typedef  int32_w  WIN_sr;   /* 0 < sr < 2^20 */

/* WIN system macros */
#include <w_macros.h>

/* WIN system struct declarations */
#include <w_struct.h>

/* WIN system constant */
#include <w_const.h>

#include <ls7000.h>
#include <ls8tel.h>
#include <winpickfile.h>

/** prototypes **/
#include <w_proto.h>
_EOF_

	if test x$ac_cv_header_sys_mtio_h = xyes; then
	   cat >> $outfile << _EOF_
int mt_pos(int, int, int);
int read_exb1(char [], int, uint8_w *, size_t);
_EOF_
	fi

	cat >> $outfile << _EOF_

#endif  /* _WINSYSTEM_H_ */
_EOF_

	if cmp -s $outfile src/winsystem.h; then
	  AC_MSG_NOTICE([src/winsystem.h is unchanged])
	  rm -f $outfile
	else
	  mv $outfile src/winsystem.h
	fi

],[
ts_cv_ipv6=$ts_cv_ipv6
ac_cv_c_char_unsigned=$ac_cv_c_char_unsigned
ac_cv_sizeof_char=$ac_cv_sizeof_char
ac_cv_sizeof_short=$ac_cv_sizeof_short
ac_cv_sizeof_int=$ac_cv_sizeof_int
ac_cv_header_sys_mtio_h=$ac_cv_header_sys_mtio_h
])

dnl -------------------------------------------------------------------------
dnl output Makefile
dnl -------------------------------------------------------------------------
AC_CONFIG_FILES([Makefile src/Makefile man/Makefile etc/Makefile pick/Makefile
	tools/Makefile tools/TKY2JGD/Makefile man.en/Makefile
	prog/Makefile
	prog/w2m
	prog/win_m
	prog/shmx
	prog/shmz
	prog/winsimu
	prog/shmck
        prog/datej.pl
	prog/ch2mf
	prog/ee_manager
	prog/final2mf
	prog/wincd
	prog/pick2pick
	prog/cpraw
	prog/wincat
	prog/shmsimu
	prog/ck_ls8800cst
	])
AC_OUTPUT
